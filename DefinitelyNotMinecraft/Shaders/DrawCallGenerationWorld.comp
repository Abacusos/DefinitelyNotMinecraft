
#extension GL_EXT_shader_8bit_storage : require
#extension GL_KHR_shader_subgroup_ballot: enable
#extension GL_KHR_shader_subgroup_arithmetic: enable

struct DrawCall
{
      uint vertexCount;
      uint instanceCount;
      uint firstVertex;
      uint firstInstance;
};

layout (std140, binding = 0) readonly uniform projectionBuffer
{
    mat4 projection;
};

layout (std140, binding = 1) readonly uniform viewBuffer
{
    mat4 view;
    vec4 cameraPos;
};

layout (std140, binding = 2) buffer transformBuffer
{
    vec4 transforms[];
};

layout (std430, binding = 3) buffer blockTypeBuffer
{
    uint8_t blockType[];
};

layout (std430, binding = 4) readonly buffer worldDataBuffer
{
    uint8_t blockTypeWorld[];
};

layout (binding = 5) buffer drawCallBuffer
{
    DrawCall drawcall;
};

layout (binding = 6) readonly uniform chunkConstants
{
    int chunkLoadCount;
    int chunksOneDimension;
    int chunkLocalSize;
    int chunkHeight;
};

layout (binding = 7) readonly buffer chunkIndexRemap
{
    uint remapIndex[];
};

struct Plane
{
    vec3 normal;
    float dist;
};

layout (binding = 8) readonly uniform cullingData
{
	Plane planes[6];
	uint cullingEnabled;
};

vec3 indexToPos()
{
  int remap = int(remapIndex[gl_WorkGroupID.x]);
  int remapOffsetX = (remap % chunksOneDimension) - chunkLoadCount;
  int remapOffsetZ = (remap / chunksOneDimension) - chunkLoadCount;
  int chunkStartX = int(chunkLocalSize * (remapOffsetX + int(cameraPos.x / chunkLocalSize)));
  int chunkStartZ = int(chunkLocalSize * (remapOffsetZ + int(cameraPos.z / chunkLocalSize)));
  int x = int(gl_LocalInvocationID.x) + chunkStartX;
  int z = int(gl_LocalInvocationID.z) + chunkStartZ;

  return vec3(x, gl_WorkGroupID.y, z);
}

uint toFlatIndex()
{
  int remap = int(remapIndex[gl_WorkGroupID.x]);
  int chunkStartX = (remap % chunksOneDimension);
  int chunkStartZ = (remap / chunksOneDimension);
  
  int heightOffset = chunkLocalSize * chunkLocalSize * int(gl_WorkGroupID.y);
  
  int sizeOfChunk = chunkLocalSize * chunkLocalSize * chunkHeight;
  int offsetPreviousChunk = chunkStartX * sizeOfChunk + chunkStartZ * chunksOneDimension * sizeOfChunk;
   
  int inLayerOffset = (int(gl_LocalInvocationID.z) % chunkLocalSize) * chunkLocalSize + (int(gl_LocalInvocationID.x) % chunkLocalSize);

  return offsetPreviousChunk + heightOffset + inLayerOffset;
}

float getSignedDistanceToPlane(const vec3 point, const Plane plane)
{
    return dot(plane.normal, point) - plane.dist;
}

bool isVisible(vec3 center, float extent)
{
	bool visible = true;

    for(int i = 0; i < 6; ++i)
    {	
        const float r = dot(abs(planes[i].normal), vec3(extent));
        visible = visible && -r <= getSignedDistanceToPlane(center, planes[i]);
    }

	visible = visible || cullingEnabled == 0;
    return visible;
}

layout (local_size_x = LOCAL_SIZE_X, local_size_y = LOCAL_SIZE_Y, local_size_z = LOCAL_SIZE_Z) in;

uint highestBitSet = uint(128);

void main()
{
  uint blockIndex = toFlatIndex();

  uint block = uint(blockTypeWorld[blockIndex]);
  if(block == uint(255) || (block & uint(128)) != 0u)
  {
    return;
  }
  
  vec3 position = indexToPos();

  uint instanceVisible = isVisible(vec3(position.x + 0.5f, position.y + 0.5f, position.z + 0.5f), 0.5f) ? 1 : 0;

  uint localIndex = subgroupExclusiveAdd(instanceVisible);

  uint highestActiveID = subgroupBallotFindMSB(subgroupBallot(true));
  uint globalIndex = 0;

    // If we're the highest active ID
  if (highestActiveID == gl_SubgroupInvocationID) {
    // We need to carve out a slice of out_triangles for our triangle
    uint localSize = localIndex + instanceVisible;

    globalIndex = atomicAdd(drawcall.instanceCount, localSize);
  }
  
  globalIndex = subgroupMax(globalIndex);

  if(instanceVisible > 0){
      blockType[globalIndex + localIndex] = uint8_t(block);
      transforms[globalIndex + localIndex] = vec4(position.x, position.y, position.z, 1.0f); 
  }
}
